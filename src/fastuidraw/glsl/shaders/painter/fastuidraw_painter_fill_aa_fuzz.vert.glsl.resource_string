vec4
fastuidraw_gl_vert_main(in uint sub_shader,
                        in uvec4 uprimary_attrib,
                        in uvec4 usecondary_attrib,
                        in uvec4 uint_attrib,
                        in uint shader_data_offset,
                        out int z_add)
{
  vec4 position_normal;
  vec3 clip_direction, clip_p;
  vec2 n, p;
  float dist, sgn;

  position_normal = uintBitsToFloat(uprimary_attrib);
  sgn = uintBitsToFloat(usecondary_attrib.x);
  position_normal.zw *= sgn;

  clip_p = fastuidraw_item_matrix * vec3(position_normal.xy, 1.0);
  n = fastuidraw_align_normal_to_screen(clip_p, position_normal.zw);
  clip_direction = fastuidraw_item_matrix * vec3(n, 0.0);
  dist = fastuidraw_local_distance_from_pixel_distance(1.0, clip_p, clip_direction);
  p = position_normal.xy + dist * n;

  fastuidraw_aa_fuzz = sgn;
  z_add = int(usecondary_attrib.y);

  return p.xyxy;
}
