/*!
 * \file fastuidraw_painter_auxiliary_image_buffer.glsl.resource_string
 * \brief file fastuidraw_painter_auxiliary_image_buffer.glsl.resource_string
 *
 * Copyright 2018 by Intel.
 *
 * Contact: kevin.rogovin@intel.com
 *
 * This Source Code Form is subject to the
 * terms of the Mozilla Public License, v. 2.0.
 * If a copy of the MPL was not distributed with
 * this file, You can obtain one at
 * http://mozilla.org/MPL/2.0/.
 *
 * \author Kevin Rogovin <kevin.rogovin@intel.com>
 *
 */


#if defined(FASTUIDRAW_PAINTER_AUXILIARY_BUFFER_INTERLOCK) || defined(FASTUIDRAW_PAINTER_AUXILIARY_BUFFER_INTERLOCK_MAIN_ONLY)

  #ifndef GL_ES
    layout(binding = FASTUIDRAW_PAINTER_AUXILIARY_BUFFER_BINDING, r8) coherent restrict lowp uniform image2D fastuidraw_auxiliary_buffer;
    #define fastuidraw_auxiliary_bufferLoad(XY) imageLoad(fastuidraw_auxiliary_buffer, XY)
    #define fastuidraw_auxiliary_bufferStore(XY, V) imageStore(fastuidraw_auxiliary_buffer, XY, V)
  #else
    layout(binding = FASTUIDRAW_PAINTER_AUXILIARY_BUFFER_BINDING, r8) coherent lowp readonly uniform image2D fastuidraw_auxiliary_buffer_read;
    layout(binding = FASTUIDRAW_PAINTER_AUXILIARY_BUFFER_BINDING, r8) coherent lowp writeonly uniform image2D fastuidraw_auxiliary_buffer_write;
    #define fastuidraw_auxiliary_bufferLoad(XY) imageLoad(fastuidraw_auxiliary_buffer_read, XY)
    #define fastuidraw_auxiliary_bufferStore(XY, V) imageStore(fastuidraw_auxiliary_buffer_write, XY, V)
  #endif

  #if !defined(FASTUIDRAW_PAINTER_AUXILIARY_BUFFER_INTERLOCK_MAIN_ONLY)
    bool fastuidraw_auxiliary_interlock_called = false;
    #define FASTUIDRAW_BEGIN_AUX_LOCK fastuidraw_auxiliary_interlock_called = true; fastuidraw_begin_interlock()
    #define FASTUIDRAW_END_AUX_LOCK   fastuidraw_end_interlock()
  #else
    #define FASTUIDRAW_BEGIN_AUX_LOCK fastuidraw_do_nothing()
    #define FASTUIDRAW_END_AUX_LOCK   fastuidraw_do_nothing()
    #define FASTUIDRAW_AUX_NEEDS_MAIN_INTERLOCK
  #endif


  float
  fastuidraw_clear_auxiliary(void)
  {
    float r;
    ivec2 pos = ivec2(gl_FragCoord.xy);

    FASTUIDRAW_BEGIN_AUX_LOCK;
    r = fastuidraw_auxiliary_bufferLoad(pos).x;
    fastuidraw_auxiliary_bufferStore(pos, vec4(0.0, 0.0, 0.0, 0.0));
    FASTUIDRAW_END_AUX_LOCK;
    return r;
  }

  void
  fastuidraw_max_auxiliary(in float f)
  {
    ivec2 pos = ivec2(gl_FragCoord.xy);

    f = clamp(f, 0.0, 1.0);
    FASTUIDRAW_BEGIN_AUX_LOCK;
    f = max(fastuidraw_auxiliary_bufferLoad(pos).x, f);
    fastuidraw_auxiliary_bufferStore(pos, vec4(f, 0.0, 0.0, 0.0));
    FASTUIDRAW_END_AUX_LOCK;
  }

  #undef FASTUIDRAW_BEGIN_AUX_LOCK
  #undef FASTUIDRAW_END_AUX_LOCK

#elif defined(FASTUIDRAW_PAINTER_AUXILIARY_BUFFER_ATOMIC)

  layout(binding = FASTUIDRAW_PAINTER_AUXILIARY_BUFFER_BINDING, r32ui) highp coherent restrict uniform uimage2D fastuidraw_auxiliary_buffer;

  float
  fastuidraw_clear_auxiliary(void)
  {
    uint r;
    ivec2 pos = ivec2(gl_FragCoord.xy);
    r = imageAtomicExchange(fastuidraw_auxiliary_buffer, pos, 0u);
    return float(r) / 255.0;
  }

  void
  fastuidraw_max_auxiliary(in float f)
  {
    ivec2 pos = ivec2(gl_FragCoord.xy);
    uint r;

    r = uint(255.0 * clamp(f, 0.0, 1.0));
    imageAtomicMax(fastuidraw_auxiliary_buffer, pos, r);
  }

#elif defined(FASTUIDRAW_PAINTER_AUXILIARY_BUFFER_FRAMEBUFFER_FETCH)

  layout(location = 1) inout float fastuidraw_auxiliary_buffer_inout;

  float
  fastuidraw_clear_auxiliary(void)
  {
    float r;
    r = fastuidraw_auxiliary_buffer_inout;
    fastuidraw_auxiliary_buffer_inout = 0.0;
    return r;
  }

  void
  fastuidraw_max_auxiliary(in float f)
  {
    f = clamp(f, 0.0, 1.0);
    fastuidraw_auxiliary_buffer_inout = max(fastuidraw_auxiliary_buffer_inout, f);
  }

#else

  float
  fastuidraw_clear_auxiliary(void)
  {
    return 0.0;
  }

  void
  fastuidraw_max_auxiliary(in float f)
  {
  }

#endif
