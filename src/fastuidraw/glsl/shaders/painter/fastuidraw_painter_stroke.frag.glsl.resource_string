vec4
fastuidraw_gl_frag_main(in uint sub_shader,
                        in uint shader_data_offset)
{
  float alpha;
  uint render_pass, dash_style;

  render_pass = FASTUIDRAW_EXTRACT_BITS(fastuidraw_stroke_sub_shader_render_pass_bit0,
                                        fastuidraw_stroke_sub_shader_render_pass_num_bits,
                                        sub_shader);

  alpha = 1.0;

  #ifdef FASTUIDRAW_STROKE_DASHED
  if((fastuidraw_stroking_packed_data & uint(fastuidraw_stroke_skip_dash_computation_mask)) == 0u)
    {
      float d, q, fw;
      uint interval_id, dash_pattern_at, dash_style;
      fastuidraw_dashed_stroking_params_header stroke_params;

      dash_style = FASTUIDRAW_EXTRACT_BITS(fastuidraw_stroke_sub_shader_dash_style_bit0,
                                           fastuidraw_stroke_sub_shader_dash_style_num_bits,
                                           sub_shader);

      /* stroke_params takes 4 elements
       */
      dash_pattern_at = shader_data_offset + uint(fastuidraw_stroke_dashed_stroking_params_header_num_blocks);

      /* read the header data (includes the stroking width and total length
         of the dash pattern).
       */
      fastuidraw_read_dashed_stroking_params_header(shader_data_offset, stroke_params);

      q = fastuidraw_compute_dash_interval(dash_pattern_at,
                                           stroke_params.total_length,
                                           stroke_params.first_interval_start,
                                           fastuidraw_stroking_distance,
                                           interval_id);

      fw = fwidth(fastuidraw_stroking_distance);
      if(dash_style == uint(fastuidraw_stroke_dashed_rounded_caps))
        {
          float y, r;
          r = stroke_params.width * 0.5;
          y = r * fastuidraw_stroking_on_boundary;
          if(q < r)
            {
              float f, sq;
              sq = sqrt(q * q + y * y);
              f = r - sq;
              if(f > q)
                {
                  fw *= abs(q) / sq;
                  q = f;
                }
            }
        }
      else if(dash_style == uint(fastuidraw_stroke_dashed_square_caps))
        {
          q += stroke_params.width * 0.5;
        }

      d = max(abs(q), fw);
      alpha = max(0.0, q / d);

      /* Rather than discarding on opaque passes, we could
         instead render in a single pass and be Ok with
         having anti-alias smudge sometimes occlude
         non-anti-aliased fragments. Doing this requires
         that means that we for anti-aliased rendering
         there is only a single rendering pass.

         Another approach is to have two passes where
         the 1st pass writes to a coverage buffer with
         a "blend mode" of MAX and the 2nd pass reads
         the coverage value, resets it to zero, and emits
         that as alpha. This approach, however requires
         either framebuffer_fetch OR fragment_shader_interlock.
       */
      if(render_pass == uint(fastuidraw_stroke_opaque_pass) && alpha < 1.0 - 1.0 / 255.0)
        {
          FASTUIDRAW_DISCARD;
        }

      if(render_pass == uint(fastuidraw_stroke_non_aa) && q < 0.0)
        {
          FASTUIDRAW_DISCARD;
        }

      if(render_pass != uint(fastuidraw_stroke_aa_pass))
        {
          alpha = 1.0;
        }
    }
  #endif

  if(render_pass == uint(fastuidraw_stroke_aa_pass))
    {
      float dd, q;

      q = 1.0 - fastuidraw_stroking_on_boundary;
      dd = max(q, fwidth(q));
      alpha *= q / dd;
    }
  else
    {
      alpha = 1.0;
    }

  return vec4(1.0, 1.0, 1.0, alpha);
}
