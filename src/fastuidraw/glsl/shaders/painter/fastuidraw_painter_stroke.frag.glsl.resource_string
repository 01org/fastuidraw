#ifndef FASTUIDRAW_STROKE_COMPUTE_DASH_INTERVAL_DEFINED
#define FASTUIDRAW_STROKE_COMPUTE_DASH_INTERVAL_DEFINED
float
fastuidraw_stroke_compute_dash_interval(in uint dashed_stroking_data_location,
                                        in float total_distance,
                                        in float first_interval_start,
                                        in float in_distance,
                                        in float distance_from_edge_start,
                                        in float distance_to_end_of_edge,
                                        in float stroke_width)
{
  int interval_id, interval_period;
  float interval_begin, interval_end, d0, d1, s, r;

  s = fastuidraw_compute_interval(dashed_stroking_data_location,
                                  total_distance,
                                  first_interval_start,
                                  in_distance, interval_period,
                                  interval_id,
                                  interval_begin,
                                  interval_end);
  r = stroke_width;

  d0 = in_distance - interval_begin;
  d1 = interval_end - in_distance;
  r = (d0 <= distance_from_edge_start) ?
    min(d0, r) :
    r;
  r = (d1 <= distance_to_end_of_edge) ?
    min(d1, r) :
    r;
  return s * r;
}
#endif

vec4
fastuidraw_gl_frag_main(in uint sub_shader,
                        in uint shader_data_offset)
{
  float alpha;
  uint render_pass, dash_style;

  render_pass = FASTUIDRAW_EXTRACT_BITS(fastuidraw_stroke_sub_shader_render_pass_bit0,
                                        fastuidraw_stroke_sub_shader_render_pass_num_bits,
                                        sub_shader);

  alpha = 1.0;

  #ifdef FASTUIDRAW_STROKE_DASHED
  if((fastuidraw_stroking_packed_data & uint(fastuidraw_stroke_join_mask)) == 0u)
    {
      float d, q, fw;
      uint dash_pattern_at, dash_style;
      fastuidraw_dashed_stroking_params_header stroke_params;

      dash_style = FASTUIDRAW_EXTRACT_BITS(fastuidraw_stroke_sub_shader_dash_style_bit0,
                                           fastuidraw_stroke_sub_shader_dash_style_num_bits,
                                           sub_shader);

      /* stroke_params takes 4 elements
       */
      dash_pattern_at = shader_data_offset + uint(fastuidraw_stroke_dashed_stroking_params_header_num_blocks);

      /* read the header data (includes the stroking width and total length
         of the dash pattern).
       */
      fastuidraw_read_dashed_stroking_params_header(shader_data_offset, stroke_params);

      if((fastuidraw_stroking_packed_data & uint(fastuidraw_stroke_cap_join_ending_mask)) != 0u)
        {
          q = fastuidraw_stroking_distance_from_edge_start;
        }
      else
        {
          q = fastuidraw_stroke_compute_dash_interval(dash_pattern_at,
                                                      stroke_params.total_length,
                                                      stroke_params.first_interval_start,
                                                      fastuidraw_stroking_distance,
                                                      fastuidraw_stroking_distance_from_edge_start,
                                                      fastuidraw_stroking_distance_to_end_of_edge,
                                                      stroke_params.width);
        }

      fw = fwidth(fastuidraw_stroking_distance);

      /* the value of q is branchy-pixel-dependent, the
         code path if we are to hit rounded-cap code is
         dependent on the value of q. To keep the anti-aliasing
         from going ugly at boundaries, we explicitely compute
         fwidth() from values that are not branchy-pixel-dependent.
       */
      float y, r, qq_yy, fwidth_qq_yy;
      r = stroke_params.width * 0.5;
      y = r * fastuidraw_stroking_on_boundary;
      qq_yy = q * q + y * y;
      fwidth_qq_yy = 2.0 * abs(q) * fw + 2.0 * abs(y) * fwidth(y);
      if(dash_style == uint(fastuidraw_stroke_dashed_rounded_caps))
        {
          /* fw represents the tolerance of a pixel. The expected
             condition is if(q < 0.0), but that gives black-line
             render issue on the boundary between the cap and the
             dash-pattern boundary. By relaxing the condition by
             (essentially) 1-pixel, we can avoid that rendering
             issue.
           */
          if(q < fw && q > -fw - r)
            {
              float sq;

              sq = sqrt(qq_yy);
              q = r - sq;
              fw = 0.5 * fwidth_qq_yy / sq;
            }
        }
      else if(dash_style == uint(fastuidraw_stroke_dashed_square_caps))
        {
          q += stroke_params.width * 0.5;
        }

      d = max(abs(q), fw);
      alpha = max(0.0, q / d);

      if(render_pass == uint(fastuidraw_stroke_non_aa) && q < 0.0)
        {
          FASTUIDRAW_DISCARD;
        }
    }
  #endif

  #ifdef FASTUIDRAW_STROKE_USE_DISCARD
    {
      float dd, q;
      q = 1.0 - fastuidraw_stroking_on_boundary;
      dd = max(q, fwidth(q));
      alpha *= q / dd;

      /* Rather than discarding on opaque passes, we could
         instead render in a single pass and be Ok with
         having anti-alias smudge sometimes occlude
         non-anti-aliased fragments. Doing this requires
         that means that we for anti-aliased rendering
         there is only a single rendering pass.

         Another approach is to have two passes where
         the 1st pass writes to a coverage buffer with
         a "blend mode" of MAX and the 2nd pass reads
         the coverage value, resets it to zero, and emits
         that as alpha. This approach, however requires
         either framebuffer_fetch OR fragment_shader_interlock.
       */
      if(render_pass == uint(fastuidraw_stroke_opaque_pass) && alpha < 1.0 - 1.0 / 255.0)
        {
          FASTUIDRAW_DISCARD;
        }

      if(render_pass != uint(fastuidraw_stroke_aa_pass))
        {
          alpha = 1.0;
        }
    }
  #else
    {
      if(render_pass == uint(fastuidraw_stroke_aa_pass))
        {
          float dd, q;

          q = 1.0 - fastuidraw_stroking_on_boundary;
          dd = max(q, fwidth(q));
          alpha *= q / dd;
        }
      else
        {
          alpha = 1.0;
        }
    }
  #endif

  return vec4(1.0, 1.0, 1.0, alpha);
}
